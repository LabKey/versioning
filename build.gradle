buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.1.1"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ajoberstar:grgit:1.6.0'
    compile 'org.tmatesoft.svnkit:svnkit:1.8.12'
    compile 'junit:junit:4.7'
    compile 'org.apache.directory.studio:org.apache.commons.io:2.4'
    compile 'apache-lang:commons-lang:2.1'
}

project.version = "1.0-SNAPSHOT"

/**
 * Plug-in publication
 */
publishing {
    publications {
        mavenJava (MavenPublication) {
            groupId 'org.labkey'
            artifactId 'versioning'
            from components.java
        }
    }
}

if (project.hasProperty('artifactory_contextUrl') && project.hasProperty('artifactory_user') && project.hasProperty('artifactory_password'))
{
    artifactory {
        contextUrl = "${artifactory_contextUrl}"
        publish {
            repository {
                repoKey = project.version.endsWith('-SNAPSHOT') ? 'libs-snapshot-local' : "libs-release-local"
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications ('mavenJava')
                publishPom = true
                publishIvy = false
            }
        }
        resolve {
            repository {
                repoKey = project.version.endsWith('-SNAPSHOT') ? 'libs-snapshot-local' : "libs-release-local"
            }
        }
    }
}


/**
 * Wrapper definition
 */
task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

test {
    ignoreFailures = true

    //Skips all tests
    exclude "**/*.class"

    environment "GIT_TEST_BRANCH", "feature/456-cute"
    environment "SVN_TEST_BRANCH", "feature-456-cute"
}
