buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.8.1"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ajoberstar.grgit:grgit-gradle:3.0.0'
    compile 'org.tmatesoft.svnkit:svnkit:1.9.3'
    compile 'junit:junit:4.7'
    compile 'org.apache.directory.studio:org.apache.commons.io:2.4'
    compile 'commons-lang:commons-lang:2.6'
}

project.version = "1.0.3-SNAPSHOT"

/**
 * Plug-in publication
 */
publishing {
    publications {
        mavenJava (MavenPublication) {
            groupId 'org.labkey.build'
            artifactId 'versioning'
            from components.java
        }
    }
}

if (project.hasProperty('doPublishing') && project.hasProperty('artifactory_contextUrl') && project.hasProperty('artifactory_user') && project.hasProperty('artifactory_password'))
{
    apply plugin: 'com.jfrog.artifactory'
    artifactory {
        contextUrl = "${artifactory_contextUrl}"
        publish {
            repository {
                repoKey = project.version.endsWith('-SNAPSHOT') ? 'plugins-snapshot-local' : "plugins-release-local"
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications ('mavenJava')
                publishPom = true
                publishIvy = false
            }
        }
        resolve {
            repository {
                repoKey = project.version.endsWith('-SNAPSHOT') ? 'plugins-snapshot-local' : "plugins-release"
            }
        }
    }
}


test {
    ignoreFailures = true

    //Skips all tests
    exclude "**/*.class"

    environment "GIT_TEST_BRANCH", "feature/456-cute"
    environment "SVN_TEST_BRANCH", "feature-456-cute"
}
